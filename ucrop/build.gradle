apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply from: '../mavenpush.gradle'

android {
    compileSdk 33
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 27
        versionName "2.3.0"
        namespace "com.yalantis.ucrop"

        vectorDrawables.useSupportLibrary = true

        aarMetadata {
            minCompileSdk = 21
        }
    }
    publishing {
        singleVariant('nativeRelease') {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant('nonNativeRelease') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    buildFeatures {
        buildConfig = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions += "processing_type"
    productFlavors {
        create("native") {
            dimension = "processing_type"
            versionNameSuffix = "-native"
            buildConfigField("String", "type", '"NATIVE"')
        }
        create("nonNative") {
            dimension = "processing_type"
            buildConfigField("String", "type", '"NON_NATIVE"')
        }
    }

    androidComponents {
        onVariants(selector().withFlavor('processing_type', 'nonNative')) {
            packaging.jniLibs.excludes.add('**/**.so')
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }

    resourcePrefix 'ucrop_'
}

dependencies {
    implementation "androidx.appcompat:appcompat:${androidx_appcompat_version}"
    implementation "androidx.exifinterface:exifinterface:${androidx_exifinterface_version}"
    implementation "androidx.transition:transition:${androidx_transition_version}"
    // OkHttp3 versions above 3.12.x don't support pre-Lollipop Android versions (API 21)
    implementation "com.squareup.okhttp3:okhttp:3.12.13"
}
